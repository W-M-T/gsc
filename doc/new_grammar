SPL         = ImportDecl* Decl+

Decl        = VarDecl
            | FunDecl
            | TypeSyn
            | OpDecl

ImportDecl  = 'from' AnyId 'import' ImportList

ImportList  = '*' | ImportName+

ImportName  = AnyId [as AnyId]

AnyId       = id | type_id

VarDecl     = ('var' | Type) id '=' Exp ';'

FunDecl     = id '(' [FArgs] ')' ['::' FunType] '{' VarDecl* Stmt+ '}'

OpDecl      = 'prefix' op '(' id ')' ['::' PreFunType] '{' VarDecl* Stmt+ '}'
            |  ('infixl' | 'infixr') int op '(' id ',' id ')' ['::' InfFunType] '{' VarDecl* Stmt+ '}'

TypeSyn     = 'type' type_id '=' Type

PreFunType  = Type -> Type

InfFunType  = Type Type -> Type

FunType     = Type* '->' RetType

RetType     = Type
            | 'Void'

Type        = BasicType
            | '(' Type ',' Type ')'
            | '[' Type ']'
            | type_id

BasicType   = 'Int'
            = 'Bool'
            = 'Char'

FArgs       = id (',' id)*

Stmt        = 'if' '(' Exp ')' '{' Stmt* '}' ('elif' '(' Exp ')' '{' Stmt* '}')* ['else' '{' Stmt* '}']
            | 'while' '(' Exp ')' '{' Stmt* '}'
            | 'for' '(' [ActStmt] ';' Exp ';' ActStmt ')' '{' Stmt* '}'
            | ActStmt ';'
            | 'return' [Exp] ';'
# for loop niet verplichten een condition of update te hebben?

ActStmt     = Ass 
            | FunCall

Ass         = id Field* '=' Exp

Exp         = ConvExp (op ConvExp)*

ConvExp     = id Field*
            | op Exp
            | int
            | char
            | string
            | 'False'
            | 'True'
            | '(' Exp ')'
            | FunCall
            | []

Field       = ('.hd' | '.tl' | '.fst' | '.snd' )

FunCall     = id '(' [ActArgs] ')'

ActArgs     = Exp (',' Exp)*

op          = opChar+

opChar      = '!' | '#' | '$' | '%' | '&' | '*' | '+' | '/' | '<' | '=' | '>' | '?'
            | '@' | '\' | '^' | '|' | '-' | '~' | ',' | ':'

int         = digit+

string      = '"' (ascii_without_esc_sequences | '\' escape_sequence_char)* '"'

char        = '\'' (ascii_without_esc_sequences | '\' escape_sequence_char) '\''

id          = loweralpha ('_' | alphaNum)*

type_id     = upperalpha ('_' | alphaNum)*

loweralpha  = a..z

upperalpha  = A..Z

alphaNum    = a..z | A..Z | 0..9




Still missing:
import statement (syntax needs to be decided)
