SPL         = Decl+

Decl        = VarDecl
            | FunDecl
            | TypeSyn
            | OpDecl

VarDecl     = ('var' | Type) id '=' Exp ';'

FunDecl     = id '(' [FArgs] ')' ['::' FunType] '{' VarDecl* Stmt+ '}'

OpDecl      = 'prefix' op '(' id ')' ['::' FunType] '{' VarDecl* Stmt+ '}'
            |  ('infixl' | 'infixr') digit+ op '(' id ',' id ')' ['::' FunType] '{' VarDecl* Stmt+ '}'

TypeSyn     = 'type' type_id '=' Type

FunType     = [FTypes] '->' RetType

FTypes      = Type+

RetType     = Type
            | 'Void'

Type        = BasicType
            | '(' Type ',' Type ')'
            | '[' Type ']'
            | type_id

BasicType   = 'Int'
            = 'Bool'
            = 'Char'

FArgs       = id (',' id)*

Stmt        = 'if' '(' Exp ')' '{' Stmt* '}' ('elif' '(' Exp ')' '{' Stmt* '}')* ['else' '{' Stmt* '}']
            | 'while' '(' Exp ')' '{' Stmt* '}'
            | 'for' '(' [ActStmt] ';' Exp ';' Exp ')' '{' Stmt* '}'
            | ActStmt ';'
            | 'return' [Exp] ';'

ActStmt     = Ass 
            | FunCall

Ass         = id Field '=' Exp

Exp         = ConvExp 
            | ConvExp (op ConvExp)+

ConvExp     = id Field
            | op Exp
            | int
            | char
            | string
            | 'False'
            | 'True'
            | '(' Exp ')'
            | FunCall
            | []

Field       = [('.hd' | '.tl' | '.fst' | '.snd' ) Field]

FunCall     = id '(' [ActArgs] ')'

ActArgs     = Exp (',' Exp)*

op          = opChar+

opChar      = '!' | '#' | '$' | '%' | '&' | '*' | '+' | '/' | '<' | '=' | '>' | '?'
            | '@' | '\' | '^' | '|' | '-' | '~' | ',' | ':'

int         = digit+

string      = '"' (ascii_without_esc_sequences | '\' escape_sequence_char)* '"'

char        = '\'' (ascii_without_esc_sequences | '\' escape_sequence_char) '\''

id          = loweralpha ('_' | alphaNum)*

type_id     = upperalpha ('_' | alphaNum)*

loweralpha  = a..z

upperalpha  = A..Z

alphaNum    = a..z | A..Z | 0..9




Still missing:
import statement (syntax needs to be decided)